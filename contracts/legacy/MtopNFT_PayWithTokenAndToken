// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/Counters.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";

import "./interfaces/IAvascaleOracle.sol";
import "./libraries/Decimals.sol";

contract AvascaleNFT is ERC721, ERC721Enumerable, Ownable {
    using Counters for Counters.Counter;
    using SafeERC20 for IERC20;

    /*********************
     ** Enums & Structs **
     *********************/

    enum Tier {
        _30,
        _90,
        _360
    }

    struct NFTDetails {
        Tier tier;
        uint256 expirationTimestamp;
    }

    /*************
     ** Storage **
     *************/

    Counters.Counter private _tokenIdCounter;

    address feeCollector;

    uint256 SECONDS_PER_DAY = 86400;

    IERC20 AVASCALE;
    IERC20 USDC;
    uint8 AVASCALE_DECIMALS;
    uint8 USDC_DECIMALS;

    IAvascaleOracle AVASCALE_ORACLE;
    uint8 ORACLE_DECIMALS;

    mapping(uint256 => NFTDetails) public nftDetails;

    /*****************
     ** Constructor **
     *****************/

    constructor(
        IERC20 _avascale,
        IERC20 _usdc,
        IAvascaleOracle _avascaleOracle,
        address _feeCollector
    ) ERC721("Avascale Mecha", "MECHA") {
        AVASCALE = _avascale;
        USDC = _usdc;
        AVASCALE_DECIMALS = IERC20Metadata(address(_avascale)).decimals();
        USDC_DECIMALS = IERC20Metadata(address(_usdc)).decimals();

        AVASCALE_ORACLE = _avascaleOracle;
        ORACLE_DECIMALS = _avascaleOracle.getPriceDecimals();

        feeCollector = _feeCollector;
    }

    /*************
     ** Ownable **
     *************/

    function setFeeCollector(address _newFeeCollector) external onlyOwner {
        feeCollector = _newFeeCollector;
    }

    function setAvascaleOracle(IAvascaleOracle _newAvascaleOracle) external onlyOwner {
        AVASCALE_ORACLE = _newAvascaleOracle;
    }

    function batchGift(address[] memory tos, Tier tier) public onlyOwner {
        for (uint256 i = 0; i < tos.length; i++) {
            gift(tos[i], tier);
        }
    }

    function gift(address to, Tier tier) public onlyOwner {
        uint256 tokenId = _tokenIdCounter.current();

        nftDetails[tokenId] = NFTDetails(tier, _getExpirationTimestamp(tier));

        _safeMint(to, tokenId);
        _tokenIdCounter.increment();
    }

    /************
     ** Public **
     ************/

    function buy(address to, Tier tier) public {
        uint256 tokenId = _tokenIdCounter.current();

        (uint256 _avascaleAmount, uint256 _usdcAmount) = getTierCost(tier);

        AVASCALE.safeTransferFrom(msg.sender, feeCollector, _avascaleAmount);
        USDC.safeTransferFrom(msg.sender, feeCollector, _usdcAmount);

        nftDetails[tokenId] = NFTDetails(tier, _getExpirationTimestamp(tier));

        _safeMint(to, tokenId);
        _tokenIdCounter.increment();
    }

    function getTierCost(Tier _tier)
        public
        view
        returns (uint256 _avascaleAmount, uint256 _usdcAmount)
    {
        if (_tier == Tier._30) {
            _usdcAmount = 15 * 10**USDC_DECIMALS;
            _avascaleAmount = _getAvascaleAmount(_usdcAmount);
        } else if (_tier == Tier._90) {
            _usdcAmount = 35 * 10**USDC_DECIMALS;
            _avascaleAmount = _getAvascaleAmount(_usdcAmount);
        } else {
            _usdcAmount = 125 * 10**USDC_DECIMALS;
            _avascaleAmount = _getAvascaleAmount(_usdcAmount);
        }
    }

    function supportsInterface(bytes4 interfaceId)
        public
        view
        override(ERC721, ERC721Enumerable)
        returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }

    function baseURI() public pure returns (string memory) {
        return _baseURI();
    }

    function tokenURI(uint256 tokenId)
        public
        view
        virtual
        override
        returns (string memory)
    {
        NFTDetails memory details = nftDetails[tokenId];
        require(details.expirationTimestamp != 0, "AvascaleNFT: !tokenId");
        return
            bytes(_baseURI()).length > 0
                ? string(
                    abi.encodePacked(_baseURI(), "/", _getJSON(details.tier))
                )
                : "";
    }

    /**************
     ** Internal **
     **************/

    function _baseURI() internal pure override returns (string memory) {
        return "ipfs://QmbzM7dGzuXvMQyUPBf5EGhdQeWLiptxunhP5orzY2NwMD";
    }

    function _getJSON(Tier _tier) internal pure returns (string memory) {
        if (_tier == Tier._30) {
            return "30.json";
        } else if (_tier == Tier._90) {
            return "90.json";
        } else {
            return "360.json";
        }
    }

    function _getAvascaleAmount(uint256 _usdcAmount)
        internal
        view
        returns (uint256)
    {
        uint256 _avascaleUsdcPrice = AVASCALE_ORACLE.getDestPrice();

        (
            uint256 _avascaleUsdcPriceFormatted,
            uint256 _usdcAmountFormatted,

        ) = Decimals.formatToBiggerDecimals(
                ORACLE_DECIMALS,
                USDC_DECIMALS,
                _avascaleUsdcPrice,
                _usdcAmount
            );

        uint256 _avascaleAmountFormatted = Decimals.divWithPrecision(
            _usdcAmountFormatted,
            _avascaleUsdcPriceFormatted,
            12
        );
        return
            Decimals.formatFromToDecimals(
                12,
                AVASCALE_DECIMALS,
                _avascaleAmountFormatted
            );
    }

    function _getExpirationTimestamp(Tier _tier)
        internal
        view
        returns (uint256)
    {
        if (_tier == Tier._30) {
            return block.timestamp + SECONDS_PER_DAY * 30;
        } else if (_tier == Tier._90) {
            return block.timestamp + SECONDS_PER_DAY * 90;
        } else {
            return block.timestamp + SECONDS_PER_DAY * 360;
        }
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal override(ERC721, ERC721Enumerable) {
        super._beforeTokenTransfer(from, to, tokenId);
    }
}
